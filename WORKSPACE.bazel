workspace(name = "space-game")

load("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")


# Load and configure bazel_skylib

http_archive(
    name = "bazel_skylib",
    urls = [
        "https://mirror.bazel.build/github.com/bazelbuild/bazel-skylib/releases/download/1.2.1/bazel-skylib-1.2.1.tar.gz",
        "https://github.com/bazelbuild/bazel-skylib/releases/download/1.2.1/bazel-skylib-1.2.1.tar.gz",
    ],
    sha256 = "f7be3474d42aae265405a592bb7da8e171919d74c16f082a5457840f06054728",
)

load("@bazel_skylib//:workspace.bzl", "bazel_skylib_workspace")

bazel_skylib_workspace()


# Load and configure rules_nixpkgs

http_archive(
    name = "io_tweag_rules_nixpkgs",
    sha256 = "b01f170580f646ee3cde1ea4c117d00e561afaf3c59eda604cf09194a824ff10",
    strip_prefix = "rules_nixpkgs-0.9.0",
    urls = ["https://github.com/tweag/rules_nixpkgs/archive/v0.9.0.tar.gz"],
)

load("@io_tweag_rules_nixpkgs//nixpkgs:repositories.bzl", "rules_nixpkgs_dependencies")

rules_nixpkgs_dependencies()

load("@io_tweag_rules_nixpkgs//nixpkgs:nixpkgs.bzl", "nixpkgs_git_repository", "nixpkgs_cc_configure")

nixpkgs_git_repository(
    name = "nixpkgs",
    revision = "22.05",
    sha256 = "0f8c25433a6611fa5664797cd049c80faefec91575718794c701f3b033f2db01",
)

nixpkgs_cc_configure(
    name = "nixpkgs_config_cc",
    repository = "@nixpkgs",
)


# Load and configure rules_pkg

http_archive(
    name = "rules_pkg",
    urls = [
        "https://mirror.bazel.build/github.com/bazelbuild/rules_pkg/releases/download/0.7.0/rules_pkg-0.7.0.tar.gz",
        "https://github.com/bazelbuild/rules_pkg/releases/download/0.7.0/rules_pkg-0.7.0.tar.gz",
    ],
    sha256 = "8a298e832762eda1830597d64fe7db58178aa84cd5926d76d5b744d6558941c2",
)

load("@rules_pkg//:deps.bzl", "rules_pkg_dependencies")

rules_pkg_dependencies()


# Create nixpkgs repositories for dependencies.

load("@io_tweag_rules_nixpkgs//nixpkgs:nixpkgs.bzl", "nixpkgs_package")

nixpkgs_package(
    name = "argtable",
    build_file_content = """\
load("@rules_cc//cc:defs.bzl", "cc_import")
cc_import(
    name = "argtable-import",
    hdrs = glob(["include/**/*.h"]),
    shared_library = "lib/libargtable3.so.0",
    visibility = ["//visibility:public"],
)
cc_library(
    name = "argtable",
    hdrs = glob(["include/**/*.h"]),
    includes = ["include"],
    deps = [":argtable-import"],
    visibility = ["//visibility:public"],
)
""",
    repository = "@nixpkgs",
)

nixpkgs_package(
    name = "boost",
    nix_file_content = """\
with import <nixpkgs> {
  config = { };
  overlays = [ ];
};
buildEnv {
  name = "boost";
  paths = [ boost155 boost155.dev ];
  postBuild = ''
    mkdir -p $out
  '';
}
""",
    build_file_content = """\
cc_import(
    name = "boost-system",
    hdrs = glob(["include/**/*.h"]),
    shared_library = "lib/libboost_system.so.1.55.0",
    visibility = ["//visibility:public"],
)
cc_library(
    name = "boost",
    hdrs = glob(["include/**/*.hpp"]),
    includes = ["include"],
    deps = [
        ":boost-system",
    ],
    visibility = ["//visibility:public"],
)
""",
    repository = "@nixpkgs",
)

nixpkgs_package(
    name = "cegui",
    build_file_content = """\
load("@rules_cc//cc:defs.bzl", "cc_import")
cc_import(
    name = "cegui-base",
    hdrs = glob(["include/**/*.h"]),
    shared_library = "lib/libCEGUIBase-0.so.2",
    visibility = ["//visibility:public"],
)
cc_import(
    name = "cegui-ogre",
    hdrs = glob(["include/**/*.h"]),
    shared_library = "lib/libCEGUIOgreRenderer-0.so.2",
    visibility = ["//visibility:public"],
)
cc_library(
    name = "cegui",
    hdrs = glob(["include/**/*.h"]),
    includes = ["include/cegui-0"],
    deps = [
        ":cegui-base",
        ":cegui-ogre",
    ],
    visibility = ["//visibility:public"],
)
""",
    repository = "@nixpkgs",
)

nixpkgs_package(
    name = "enet",
    build_file_content = """\
load("@rules_cc//cc:defs.bzl", "cc_import")
cc_import(
    name = "enet-import",
    hdrs = glob(["include/**/*.h"]),
    shared_library = "lib/libenet.so.7",
    visibility = ["//visibility:public"],
)
cc_library(
    name = "enet",
    hdrs = glob(["include/**/*.h"]),
    includes = ["include"],
    deps = [":enet-import"],
    visibility = ["//visibility:public"],
)
""",
    repository = "@nixpkgs",
)

nixpkgs_package(
    name = "expat",
    nix_file_content = """\
with import <nixpkgs> {
  config = { };
  overlays = [ ];
};
buildEnv {
  name = "expat";
  paths = [ expat expat.dev ];
  postBuild = ''
    mkdir -p $out
  '';
}
""",
    build_file_content = """\
load("@rules_cc//cc:defs.bzl", "cc_import")
cc_import(
    name = "expat-import",
    hdrs = glob(["include/**/*.h"]),
    shared_library = "lib/libexpat.so.1",
    visibility = ["//visibility:public"],
)
cc_library(
    name = "expat",
    hdrs = glob(["include/**/*.h"]),
    includes = ["include"],
    deps = [":expat-import"],
    visibility = ["//visibility:public"],
)
""",
    repository = "@nixpkgs",
)

nixpkgs_package(
    name = "lua",
    attribute_path = "lua5_1",
    build_file_content = """\
load("@rules_cc//cc:defs.bzl", "cc_import")
cc_import(
    name = "lua-import",
    hdrs = glob(["include/**/*.h"]),
    shared_library = "lib/liblua.so.5.1",
    visibility = ["//visibility:public"],
)
cc_library(
    name = "lua",
    hdrs = glob(["include/**/*.h"]),
    includes = ["include"],
    deps = [":lua-import"],
    visibility = ["//visibility:public"],
)
""",
    repository = "@nixpkgs",
)

nixpkgs_package(
    name = "ogre",
    attribute_path = "ogre1_10",
    build_file_content = """\
load("@rules_cc//cc:defs.bzl", "cc_import")
cc_import(
    name = "ogre-main",
    hdrs = glob(["include/**/*.h"]),
    shared_library = "lib/libOgreMain.so.1.10.11",
    visibility = ["//visibility:public"],
)
cc_import(
    name = "ogre-overlay",
    hdrs = glob(["include/**/*.h"]),
    shared_library = "lib/libOgreOverlay.so.1.10.11",
    visibility = ["//visibility:public"],
)
cc_library(
    name = "ogre",
    hdrs = glob(["include/**/*.h"]),
    includes = [
        "include",
        "include/OGRE",
        "include/OGRE/Overlay",
    ],
    deps = [
        ":ogre-main",
        ":ogre-overlay",
    ],
    visibility = ["//visibility:public"],
)
exports_files([
    "bin/OgreXMLConverter",
    "lib/OGRE",
])
""",
    repository = "@nixpkgs",
)

nixpkgs_package(
    name = "ois",
    build_file_content = """\
load("@rules_cc//cc:defs.bzl", "cc_import")
cc_import(
    name = "ois-import",
    hdrs = glob(["include/**/*.h"]),
    shared_library = "lib/libOIS.so.1.5.0",
    visibility = ["//visibility:public"],
)
cc_library(
    name = "ois",
    hdrs = glob(["include/**/*.h"]),
    includes = ["include"],
    deps = [":ois-import"],
    visibility = ["//visibility:public"],
)
""",
    repository = "@nixpkgs",
)

load("@bazel_tools//tools/build_defs/repo:git.bzl", "new_git_repository")

new_git_repository(
    name = "nixgl",
    remote = "https://github.com/guibou/nixGL",
    commit = "7165ffbccbd2cf4379b6cd6d2edd1620a427e5ae",
    shallow_since = "1661367362 +0200",
    build_file_content = "exports_files(glob([\"**/*\"]))",
)
